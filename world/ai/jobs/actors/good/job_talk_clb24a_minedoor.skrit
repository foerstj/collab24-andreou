/*
  ============================================================================
  ----------------------------------------------------------------------------

	File		: 	job_talk_clb24a_minedoor.skrit

	Author(s)	: 	Andreou-NG

	Purpose		: 	Talk skrit for the mine door with the clear automatons quest.

  ----------------------------------------------------------------------------
  ============================================================================
*/

Go		m_Go$;
GoMind	m_Mind$;
GoBody  m_Body$;
Job		m_Job$;
Goid	m_Target$;

#include "k_job_c_mcp_fidget_utils"
#include "k_job_c_talk_utils"

startup state STARTUP$
{
}

////////////////////////////////////////////////////////////////////////////////
//	init
event OnJobInitPointers$( Job job$ )
{
	m_Job$		= job$;
	m_Go$		= job$.Go;
	m_Mind$		= job$.Go.Mind;
	m_Body$		= job$.Go.Body;
}


event OnJobInit$( Job job$ )
{
	OnJobInitPointers$( job$ );

	m_Target$	= job$.GoalObject;

	SetState Talk$;
}

////////////////////////////////////////////////////////////////////////////////
//	blab blab away... pretend you're Lee on coffee
state Talk$
{
	transition
	{
		-> Exiting$:	OnWorldMessage( WE_REQ_TALK_END ) =
		{
			if( m_Go$.Mind.ActorAutoFidgets )
			{
				StartFidgetIfRequired$();
			}
		}
	}

	event OnEnterState$
	{
		if ( GameAuditor.GetDb.GetBool( "clb24a_mines_automatons_cleared" ) )
		{
			if ( GameAuditor.GetDb.GetBool( "clb24a_talked_to_mine_door" ) )
			{
				m_Go$.GetConversation.RSSelectConversation( "mine_door_complete" );
			}
			else
			{
				m_Go$.GetConversation.RSSelectConversation( "mine_door_complete_first_convo" );
			}
			GameAuditor.GetDb.SetBool( "clb24a_mine_door_unlocked", true);
		} 
		else 
		{
			m_Go$.GetConversation.RSSelectConversation( "mine_door_1" );
			GameAuditor.GetDb.SetBool( "clb24a_talked_to_mine_door", true);
		}
		m_Go$.GetConversation.RSActivateDialogue();
	}
}


////////////////////////////////////////////////////////////////////////////////
//	finish blabbing
state Exiting$
{
	event OnEnterState$
	{
		m_Job$.MarkForDeletion();
	}
}
